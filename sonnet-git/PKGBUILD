# Maintainer: Piotr GÃ³rski <lucjan.lucjanov@gmail.com>
# Contributor: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Antonio Rojas <arojas@archlinux.org>

pkgname=sonnet-git
_pkgname=sonnet
pkgver=5.64.0.r489.g561875e
pkgrel=1
pkgdesc='Spelling framework for Qt5'
arch=('x86_64')
url='https://community.kde.org/Frameworks'
license=('LGPL')
depends=('qt5-base')
makedepends=('extra-cmake-modules' 'qt5-tools' 'qt5-doc' 'hunspell' 'aspell' 'hspell' 'libvoikko' 'doxygen' 'git')
optdepends=('hunspell: spell checking via hunspell' 'aspell: spell checking via aspell' 
            'hspell: spell checking for Hebrew' 'libvoikko: Finnish support via Voikko')
provides=('sonnet')
conflicts=('sonnet')
groups=('kf5-git')
source=("git://anongit.kde.org/$_pkgname.git")
sha256sums=('SKIP')

pkgver() {
  cd $_pkgname
  _ver="$(cat CMakeLists.txt | grep -m1 'set(KF5_VERSION' | cut -d '"' -f2 | tr - .)"
  echo "${_ver}.r$(git rev-list --count HEAD).g$(git rev-parse --short HEAD)"
}
   
prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${_pkgname} \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
