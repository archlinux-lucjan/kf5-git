# Maintainer: Piotr GÃ³rski <lucjan.lucjanov@gmail.com>
# Contributor: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: FadeMind <fademind@gmail.com>

pkgname=extra-cmake-modules-git
_pkgname=extra-cmake-modules
pkgver=5.39.0.r2925.g6604694
pkgrel=1
pkgdesc='Extra modules and scripts for CMake'
arch=('any')
url='https://projects.kde.org/projects/kdesupport/extra-cmake-modules'
license=('LGPL')
depends=('cmake')
makedepends=('python-sphinx' 'python-requests' 'qt5-tools')
conflicts=('extra-cmake-modules')
provides=('extra-cmake-modules')
source=('git://anongit.kde.org/extra-cmake-modules.git'
fix-python-bindings-generation.patch ECM-no-init.py.patch)
sha1sums=('SKIP'
          'f5a4f82e1f82382ddcd09d49bac73ed12ed8aec6'
          '7ca60747096c1816302edf5b21a9bb3bea55995b')

pkgver() {
  cd extra-cmake-modules
  _ver="$(cat CMakeLists.txt | grep -m1 'set(VERSION' | cut -d '"' -f2 | tr - .)"
  echo "${_ver}.r$(git rev-list --count HEAD).g$(git rev-parse --short HEAD)"
}

prepare() {
  mkdir -p build
  cd $_pkgname
  sed -e 's|/usr/bin/env python|/usr/bin/env python2|' -i find-modules/*.py
# Fix Ubuntu-specific code https://bugs.kde.org/show_bug.cgi?id=372311
  patch -p1 -i ../fix-python-bindings-generation.patch
# Don't create __init__.py, depend on python-pykf5 instead
  patch -p1 -i ../ECM-no-init.py.patch
}

build() {
  cd build
  cmake ../extra-cmake-modules \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_HTML_DOCS=OFF \
    -DBUILD_QTHELP_DOCS=ON \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
