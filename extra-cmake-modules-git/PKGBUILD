# Maintainer: Piotr GÃ³rski <lucjan.lucjanov@gmail.com>
# Contributor: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: FadeMind <fademind@gmail.com>

pkgname=extra-cmake-modules-git
_pkgname=extra-cmake-modules
pkgver=5.85.0.r3357.g602e4816
pkgrel=1
pkgdesc='Extra modules and scripts for CMake'
arch=('any')
url='https://projects.kde.org/projects/kdesupport/extra-cmake-modules'
license=('LGPL')
groups=('kf5-git')
depends=('cmake')
makedepends=('python-sphinx' 'python-requests' 'qt5-tools' 'git')
conflicts=('extra-cmake-modules')
provides=('extra-cmake-modules')
source=("git+https://anongit.kde.org/$_pkgname.git"
        'ECM-no-init.py.patch')
sha1sums=('SKIP'
          '7ca60747096c1816302edf5b21a9bb3bea55995b')

pkgver() {
  cd $_pkgname
  _ver="$(cat CMakeLists.txt | grep -m1 'set(VERSION' | cut -d '"' -f2 | tr - .)"
  echo "${_ver}.r$(git rev-list --count HEAD).g$(git rev-parse --short HEAD)"
}

prepare() {
  patch -d $_pkgname -p1 < ECM-no-init.py.patch # Don't create __init__.py
}

build() {
  cmake -B build -S $_pkgname \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_HTML_DOCS=ON \
    -DBUILD_QTHELP_DOCS=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
