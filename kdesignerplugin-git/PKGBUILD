# Maintainer: Piotr GÃ³rski <lucjan.lucjanov@gmail.com>
# Contributor: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdesignerplugin-git
_pkgname=kdesignerplugin
pkgver=5.39.0.r180.gb50a80c
pkgrel=1
pkgdesc='Integration of Frameworks widgets in Qt Designer/Creator'
arch=('x86_64')
url='https://community.kde.org/Frameworks'
license=('LGPL')
depends=(kplotting kio) # kdewebkit https://bugs.archlinux.org/task/54540
makedepends=(extra-cmake-modules qt5-tools kdoctools python)
provides=('kdesignerplugin')
conflicts=('kdesignerplugin')
groups=('kf5-git')
source=("git://anongit.kde.org/$_pkgname.git")
sha256sums=('SKIP')

pkgver() {
  cd $_pkgname
  _ver="$(cat CMakeLists.txt | grep -m1 'set(KF5_VERSION' | cut -d '"' -f2 | tr - .)"
  echo "${_ver}.r$(git rev-list --count HEAD).g$(git rev-parse --short HEAD)"
}
   
            
prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${_pkgname} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF \
    -DKF5WebKit_FOUND=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
