# Maintainer: Piotr GÃ³rski <lucjan.lucjanov@gmail.com>
# Contributor: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdesignerplugin-git
_pkgname=kdesignerplugin
pkgver=5.72.0.r284.g02b377a
pkgrel=1
pkgdesc='Integration of Frameworks widgets in Qt Designer/Creator'
arch=('x86_64')
url='https://community.kde.org/Frameworks'
license=('LGPL')
depends=('kconfig' 'kcoreaddons')
makedepends=('extra-cmake-modules' 'qt5-tools' 'kdoctools' 'git')
provides=('kdesignerplugin')
conflicts=('kdesignerplugin')
groups=('kf5-aids-git')
source=("git+https://anongit.kde.org/$_pkgname.git")
sha256sums=('SKIP')

pkgver() {
  cd $_pkgname
  _ver="$(cat CMakeLists.txt | grep -m1 'set(KF5_VERSION' | cut -d '"' -f2 | tr - .)"
  echo "${_ver}.r$(git rev-list --count HEAD).g$(git rev-parse --short HEAD)"
}

build() {
  cmake -B build -S $_pkgname \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
