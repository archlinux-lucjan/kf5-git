# Maintainer: Piotr GÃ³rski <lucjan.lucjanov@gmail.com>
# Contributor: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Antonio Rojas <arojas@archlinux.org>

pkgname=syntax-highlighting-git
_pkgname=syntax-highlighting
pkgver=5.39.0.r546.g6ff9044
pkgrel=1
pkgdesc='Syntax highlighting engine for structured text and code'
arch=(i686 x86_64)
url='https://community.kde.org/Frameworks'
license=(GPL2)
depends=(qt5-base)
makedepends=(extra-cmake-modules qt5-xmlpatterns qt5-tools)
provides=('syntax-highlighting')
conflicts=('syntax-highlighting')
groups=(kf5-git)
source=("git://anongit.kde.org/$_pkgname.git"
        pkgbuild-syntax-highlight.patch)
sha256sums=('SKIP'
            '4321ee61f2465966f87dde89cf60e39b30de14e82b492d9a11a5c7e725b1ad8f')

pkgver() {
  cd $_pkgname
  _ver="$(cat CMakeLists.txt | grep -m1 'set(KF5_VERSION' | cut -d '"' -f2 | tr - .)"
  echo "${_ver}.r$(git rev-list --count HEAD).g$(git rev-parse --short HEAD)"
}
   
            
prepare() {
  mkdir -p build
  # Add PKGBUILD syntax highlighting
  cd $_pkgname
  patch -p1 -i ../pkgbuild-syntax-highlight.patch
}


build() {
  cd build
  cmake ../${_pkgname} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_LIBDIR=lib
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
