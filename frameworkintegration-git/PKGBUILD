# Maintainer: Piotr GÃ³rski <lucjan.lucjanov@gmail.com>
# Contributor: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=frameworkintegration-git
_pkgname=frameworkintegration
pkgver=5.72.0.r523.g531afa6
pkgrel=1
pkgdesc='Framework providing components to allow applications to integrate with a KDE Workspace'
arch=('x86_64')
url='https://community.kde.org/Frameworks'
license=('LGPL')
depends=('kpackage' 'knewstuff')
makedepends=('extra-cmake-modules' 'appstream-qt' 'packagekit-qt5' 'git')
optdepends=('appstream-qt: dependency resolving via AppStream' 'packagekit-qt5: dependency resolving via AppStream')
provides=('frameworkintegration')
conflicts=('frameworkintegration')
groups=('kf5-git')
source=("git+https://anongit.kde.org/$_pkgname.git")
sha256sums=('SKIP')

pkgver() {
  cd $_pkgname
  _ver="$(cat CMakeLists.txt | grep -m1 'set(KF5_VERSION' | cut -d '"' -f2 | tr - .)"
  echo "${_ver}.r$(git rev-list --count HEAD).g$(git rev-parse --short HEAD)"
}

build() {
  cmake -B build -S $_pkgname \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
