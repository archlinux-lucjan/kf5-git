# Maintainer: Piotr GÃ³rski <lucjan.lucjanov@gmail.com>
# Contributor: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=khtml-git
_pkgname=khtml
pkgver=5.35.0.r358.g44787ea
pkgrel=1
pkgdesc='KHTML APIs'
arch=('i686' 'x86_64')
url='https://community.kde.org/Frameworks'
license=('LGPL')
depends=('giflib' 'kparts' 'kjs')
makedepends=('extra-cmake-modules' 'python' 'gperf')
provides=('khtml')
conflicts=('khtml')
groups=('kf5-aids-git')
source=("git://anongit.kde.org/$_pkgname.git")
sha256sums=('SKIP')

pkgver() {
  cd $_pkgname
  _ver="$(cat CMakeLists.txt | grep -m1 'set(KF5_VERSION' | cut -d '"' -f2 | tr - .)"
  echo "${_ver}.r$(git rev-list --count HEAD).g$(git rev-parse --short HEAD)"
}
   
            
prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${_pkgname} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
