# Maintainer: Piotr GÃ³rski <lucjan.lucjanov@gmail.com>
# Contributor: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=ktexteditor-git
_pkgname=ktexteditor
pkgver=5.79.0.r2717.gc439810e
pkgrel=1
pkgdesc='Advanced embeddable text editor'
arch=('x86_64')
url='https://community.kde.org/Frameworks'
license=('LGPL')
depends=('ki18n' 'kconfig' 'kcrash' 'kdbusaddons')
makedepends=('extra-cmake-modules' 'kdoctools' 'doxygen' 'qt5-tools' 'qt5-doc' 'git')
provides=('ktexteditor')
conflicts=('ktexteditor')
groups=('kf5-git')
source=("git+https://anongit.kde.org/$_pkgname.git")
sha256sums=('SKIP')

pkgver() {
  cd $_pkgname
  _ver="$(cat CMakeLists.txt | grep -m1 'set(KF_VERSION' | cut -d '"' -f2 | tr - .)"
  echo "${_ver}.r$(git rev-list --count HEAD).g$(git rev-parse --short HEAD)"
}

build() {
  cmake -B build -S $_pkgname \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
