# Maintainer: Piotr GÃ³rski <lucjan.lucjanov@gmail.com>
# Contributor: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Antonio Rojas <arojas@archlinux.org>

pkgname=solid-git
_pkgname=solid
pkgver=5.79.0.r637.gb53ba7c
pkgrel=1
pkgdesc='Hardware integration and detection'
arch=('x86_64')
url='https://community.kde.org/Frameworks'
license=('LGPL')
depends=('qt5-base' 'media-player-info' 'upower' 'udisks2')
makedepends=('extra-cmake-modules' 'qt5-tools' 'qt5-doc' 'doxygen' 'qt5-declarative' 'git')
optdepends=('qt5-declarative: QML bindings')
provides=('solid')
conflicts=('solid')
groups=('kf5-git')
source=("git+https://anongit.kde.org/$_pkgname.git")
sha256sums=('SKIP')

pkgver() {
  cd $_pkgname
  _ver="$(cat CMakeLists.txt | grep -m1 'set(KF_VERSION' | cut -d '"' -f2 | tr - .)"
  echo "${_ver}.r$(git rev-list --count HEAD).g$(git rev-parse --short HEAD)"
}
   
build() {
  cmake -B build -S $_pkgname \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON \
    -DWITH_NEW_POWER_ASYNC_API=ON \
    -DWITH_NEW_POWER_ASYNC_FREEDESKTOP=ON \
    -DWITH_NEW_SOLID_JOB=ON # https://bugs.archlinux.org/task/64093
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
