# Maintainer: Piotr GÃ³rski <lucjan.lucjanov@gmail.com>
# Contributor: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kwidgetsaddons-git
_pkgname=kwidgetsaddons
pkgver=5.39.0.r392.g63cf9b1
pkgrel=1
pkgdesc='Addons to QtWidgets'
arch=('i686' 'x86_64')
url='https://community.kde.org/Frameworks'
license=('LGPL')
depends=('qt5-base')
makedepends=('extra-cmake-modules' 'qt5-tools' 'clang' 'python2-pyqt5' 'python-pyqt5')
optdepends=('python-pykf5-git: to use the Python 3 bindings' 'python2-pykf5-git: to use the Python 2 bindings')
provides=('kwidgetsaddons')
conflicts=('kwidgetsaddons')
groups=('kf5-git')
source=("git://anongit.kde.org/$_pkgname.git")
sha256sums=('SKIP')

pkgver() {
  cd $_pkgname
  _ver="$(cat CMakeLists.txt | grep -m1 'set(KF5_VERSION' | cut -d '"' -f2 | tr - .)"
  echo "${_ver}.r$(git rev-list --count HEAD).g$(git rev-parse --short HEAD)"
}
   

             
prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${_pkgname} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
