# Maintainer: Piotr GÃ³rski <lucjan.lucjanov@gmail.com>
# Contributor: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kconfigwidgets-git
_pkgname=kconfigwidgets
pkgver=5.64.0.r401.ge6f115c
pkgrel=1
pkgdesc='Widgets for KConfig'
arch=('x86_64')
url='https://community.kde.org/Frameworks'
license=('LGPL')
depends=('kauth' 'kcodecs' 'kconfig' 'kguiaddons' 'ki18n' 'kwidgetsaddons')
makedepends=('extra-cmake-modules' 'kdoctools' 'clang' 'python-pyqt5' 'doxygen' 'qt5-tools' 'qt5-doc' 'sip' 'git')
optdepends=('python-pyqt5: for the Python bindings')
provides=('kconfigwidgets')
conflicts=('kconfigwidgets')
groups=('kf5-git')
source=("git://anongit.kde.org/$_pkgname.git")
sha256sums=('SKIP')

pkgver() {
  cd $_pkgname
  _ver="$(cat CMakeLists.txt | grep -m1 'set(KF5_VERSION' | cut -d '"' -f2 | tr - .)"
  echo "${_ver}.r$(git rev-list --count HEAD).g$(git rev-parse --short HEAD)"
}
   
prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${_pkgname} \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
