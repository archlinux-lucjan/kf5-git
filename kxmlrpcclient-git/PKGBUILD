# Maintainer: Piotr GÃ³rski <lucjan.lucjanov@gmail.com>
# Contributor: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# contributor: Antonio Rojas <arojas@archlinux.org>

pkgname=kxmlrpcclient-git
_pkgname=kxmlrpcclient
pkgver=5.39.0.r301.gb9361e942
pkgrel=1
pkgdesc="XML-RPC client library for KDE"
arch=('i686' 'x86_64')
url="https://projects.kde.org/projects/kde/pim/kxmlrpcclient"
license=('LGPL')
depends=('kio')
makedepends=('extra-cmake-modules' 'python' 'doxygen' 'qt5-tools')
provides=('kxmlrpcclient')
conflicts=('kxmlrpcclient')
groups=('kf5-git')
source=("git://anongit.kde.org/$_pkgname.git")
sha256sums=('SKIP')

pkgver() {
  cd $_pkgname
  _ver="$(cat CMakeLists.txt | grep -m1 'set(KF5_VERSION' | cut -d '"' -f2 | tr - .)"
  echo "${_ver}.r$(git rev-list --count HEAD).g$(git rev-parse --short HEAD)"
}
   

            
prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${_pkgname} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
