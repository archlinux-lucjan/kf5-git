# Maintainer: Piotr GÃ³rski <lucjan.lucjanov@gmail.com>
# Contributor: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# contributor: Antonio Rojas <arojas@archlinux.org>

pkgname=kxmlrpcclient-git
_pkgname=kxmlrpcclient
pkgver=5.79.0.r424.g754706e4a
pkgrel=1
pkgdesc="XML-RPC client library for KDE"
arch=('x86_64')
url="https://projects.kde.org/projects/kde/pim/kxmlrpcclient"
license=('LGPL')
depends=('kio')
makedepends=('extra-cmake-modules' 'doxygen' 'qt5-tools' 'qt5-doc' 'git')
provides=('kxmlrpcclient')
conflicts=('kxmlrpcclient')
groups=('kf5-aids-git')
source=("git+https://anongit.kde.org/$_pkgname.git")
sha256sums=('SKIP')

pkgver() {
  cd $_pkgname
  _ver="$(cat CMakeLists.txt | grep -m1 'set(KF_VERSION' | cut -d '"' -f2 | tr - .)"
  echo "${_ver}.r$(git rev-list --count HEAD).g$(git rev-parse --short HEAD)"
}

build() {
  cmake -B build -S $_pkgname \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
