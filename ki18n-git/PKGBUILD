# Maintainer: Piotr GÃ³rski <lucjan.lucjanov@gmail.com>
# Contributor: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=ki18n-git
_pkgname=ki18n
pkgver=5.64.0.r349.gb45c3ce
pkgrel=1
pkgdesc='Advanced internationalization framework'
arch=('x86_64')
url='https://community.kde.org/Frameworks'
license=('LGPL')
depends=('qt5-declarative')
makedepends=('extra-cmake-modules' 'qt5-declarative' 'python-pyqt5' 'clang' 'doxygen' 'qt5-tools' 'qt5-doc' 'sip' 'git')
optdepends=('python-pyqt5: for the Python bindings' 'python: to compile .ts files')
provides=('ki18n')
conflicts=('ki18n')
groups=('kf5-git')
source=("git://anongit.kde.org/$_pkgname.git")
sha256sums=('SKIP')

pkgver() {
  cd $_pkgname
  _ver="$(cat CMakeLists.txt | grep -m1 'set(KF5_VERSION' | cut -d '"' -f2 | tr - .)"
  echo "${_ver}.r$(git rev-list --count HEAD).g$(git rev-parse --short HEAD)"
}
   
prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${_pkgname} \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
