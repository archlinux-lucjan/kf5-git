# Maintainer: Piotr GÃ³rski <lucjan.lucjanov@gmail.com>
# Contributor: Antonio Rojas <arojas@archlinux.org>

pkgname=syndication-git
_pkgname=syndication
pkgver=5.64.0.r725.g5b014b8
pkgrel=1
pkgdesc="RSS/Atom parser library"
arch=('x86_64')
url="https://community.kde.org/Frameworks"
license=('LGPL')
depends=('kcodecs')
makedepends=('extra-cmake-modules' 'doxygen' 'qt5-tools' 'qt5-doc')
groups=('kf5-git')
conflicts=('kde-syndication' 'syndication')
provides=('kde-syndication' 'syndication')
replaces=('kde-syndication')
source=("git://anongit.kde.org/$_pkgname.git")
sha256sums=('SKIP')

pkgver() {
  cd $_pkgname
  _ver="$(cat CMakeLists.txt | grep -m1 'set(KF5_VERSION' | cut -d '"' -f2 | tr - .)"
  echo "${_ver}.r$(git rev-list --count HEAD).g$(git rev-parse --short HEAD)"
}

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$_pkgname\
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
