# Maintainer: Piotr GÃ³rski <lucjan.lucjanov@gmail.com>
# Contributor: Antonio Rojas <arojas@archlinux.org>

pkgname=purpose-git
_pkgname=purpose
pkgver=5.43.0.r435.g21d46a9
pkgrel=1
pkgdesc="Framework for providing abstractions to get the developer's purposes fulfilled"
arch=(x86_64)
url='https://community.kde.org/Frameworks'
license=(LGPL)
depends=(kaccounts-integration accounts-qml-module hicolor-icon-theme)
makedepends=(extra-cmake-modules intltool python)
optdepends=('kdeconnect: sharing to smartphone via KDE Connect')
groups=(kf5-git)
provides=('purpose')
conflicts=('purpose')
source=("git://anongit.kde.org/$_pkgname.git")
sha256sums=('SKIP')

pkgver() {
  cd $_pkgname
  _ver="$(cat CMakeLists.txt | grep -m1 'set(KF5_VERSION' | cut -d '"' -f2 | tr - .)"
  echo "${_ver}.r$(git rev-list --count HEAD).g$(git rev-parse --short HEAD)"
}

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$_pkgname \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
