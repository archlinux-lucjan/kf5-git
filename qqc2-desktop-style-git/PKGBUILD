# Maintainer: Piotr GÃ³rski <lucjan.lucjanov@gmail.com>
# Contributor: Antonio Rojas <arojas@archlinux.org>

pkgname=qqc2-desktop-style-git
_pkgname=qqc2-desktop-style
pkgver=5.72.0.r366.gcc07ed9
pkgrel=1
pkgdesc='A style for Qt Quick Controls 2 to make it follow your desktop theme'
arch=('x86_64')
url='https://community.kde.org/Frameworks'
license=('LGPL')
depends=('kirigami2' 'kiconthemes')
makedepends=('extra-cmake-modules' 'git')
groups=('kf5-git')
provides=('qqc2-desktop-style')
conflicts=('qqc2-desktop-style')
source=("git+https://anongit.kde.org/$_pkgname.git")
sha256sums=('SKIP')

pkgver() {
  cd $_pkgname
  _ver="$(cat CMakeLists.txt | grep -m1 'set(KF5_VERSION' | cut -d '"' -f2 | tr - .)"
  echo "${_ver}.r$(git rev-list --count HEAD).g$(git rev-parse --short HEAD)"
}

build() {
  cmake -B build -S $_pkgname \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
