# Maintainer: Piotr GÃ³rski <lucjan.lucjanov@gmail.com>
# Contributor: Antonio Rojas <arojas@archlinux.org>

pkgname=kcontacts-git
_pkgname=kcontacts
epoch=1
pkgver=5.64.0.r3118.g0412a0b0
pkgrel=1
pkgdesc="Address book API for KDE"
arch=('x86_64')
url="https://community.kde.org/Frameworks"
license=('LGPL')
depends=('kcoreaddons' 'kconfig' 'ki18n' 'kcodecs' 'iso-codes')
makedepends=('extra-cmake-modules' 'doxygen' 'qt5-tools' 'qt5-doc')
provides=('kcontacts')
conflicts=('kcontacts')
groups=('kf5-git')
source=("git://anongit.kde.org/$_pkgname.git")
sha256sums=('SKIP')

pkgver() {
  cd $_pkgname
  _ver="$(cat CMakeLists.txt | grep -m1 'set(KF5_VERSION' | cut -d '"' -f2 | tr - .)"
  echo "${_ver}.r$(git rev-list --count HEAD).g$(git rev-parse --short HEAD)"
}

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$_pkgname \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
