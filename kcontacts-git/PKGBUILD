# Maintainer: Piotr GÃ³rski <lucjan.lucjanov@gmail.com>
# Contributor: Antonio Rojas <arojas@archlinux.org>

pkgname=kcontacts-git
_pkgname=kcontacts
epoch=1
pkgver=5.79.0.r3213.g384974c2
pkgrel=1
pkgdesc="Address book API for KDE"
arch=('x86_64')
url="https://community.kde.org/Frameworks"
license=('LGPL')
depends=('kcoreaddons' 'kconfig' 'ki18n' 'kcodecs' 'iso-codes')
makedepends=('extra-cmake-modules' 'doxygen' 'qt5-tools' 'qt5-doc' 'git')
provides=('kcontacts')
conflicts=('kcontacts')
groups=('kf5-git')
source=("git+https://anongit.kde.org/$_pkgname.git")
sha256sums=('SKIP')

pkgver() {
  cd $_pkgname
  _ver="$(cat CMakeLists.txt | grep -m1 'set(KF_VERSION' | cut -d '"' -f2 | tr - .)"
  echo "${_ver}.r$(git rev-list --count HEAD).g$(git rev-parse --short HEAD)"
}

build() {
  cmake -B build -S $_pkgname \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
