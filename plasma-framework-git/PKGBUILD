# Maintainer: Piotr GÃ³rski <lucjan.lucjanov@gmail.com>
# Contributor: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Antonio Rojas <arojas@archlinux.org>

pkgname=plasma-framework-git
_pkgname=plasma-framework
pkgver=5.72.0.r15449.gd0d6e73b3
pkgrel=1
pkgdesc='Plasma library and runtime components based upon KF5 and Qt5'
arch=('x86_64')
url='https://community.kde.org/Frameworks'
license=('LGPL')
depends=('kactivities' 'kdeclarative' 'kwayland' 'kirigami2')
makedepends=('extra-cmake-modules' 'qt5-tools' 'qt5-doc' 'kdoctools' 'doxygen' 'git')
provides=('plasma-framework')
conflicts=('plasma-framework')
groups=('kf5-git')
source=("git+https://anongit.kde.org/$_pkgname.git")
sha256sums=('SKIP')

pkgver() {
  cd $_pkgname
  _ver="$(cat CMakeLists.txt | grep -m1 'set(KF5_VERSION' | cut -d '"' -f2 | tr - .)"
  echo "${_ver}.r$(git rev-list --count HEAD).g$(git rev-parse --short HEAD)"
}

build() {
  cmake -B build -S $_pkgname \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
