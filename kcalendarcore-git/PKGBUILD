# Maintainer: Piotr GÃ³rski <lucjan.lucjanov@gmail.com>
# Contributor: Antonio Rojas <arojas@archlinux.org>

pkgname=kcalendarcore-git
_pkgname=kcalendarcore
pkgver=5.72.0.r1132.g57af4a3e5
pkgrel=1
pkgdesc="The KDE calendar access library"
arch=('x86_64')
url="https://community.kde.org/Frameworks"
license=('LGPL')
depends=('libical' 'qt5-base')
makedepends=('extra-cmake-modules' 'doxygen' 'qt5-tools' 'qt5-doc' 'git')
conflicts=('kcalcore' 'kcalendarcore')
provides=('kcalcore' 'kcalendarcore')
replaces=('kcalcore')
groups=('kf5-git')
source=("git+https://anongit.kde.org/$_pkgname.git")
sha256sums=('SKIP')

pkgver() {
  cd $_pkgname
  _ver="$(cat CMakeLists.txt | grep -m1 'set(KF5_VERSION' | cut -d '"' -f2 | tr - .)"
  echo "${_ver}.r$(git rev-list --count HEAD).g$(git rev-parse --short HEAD)"
}

build() {
  cmake -B build -S $_pkgname \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
