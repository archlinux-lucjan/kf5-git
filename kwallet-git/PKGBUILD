# Maintainer: Piotr GÃ³rski <lucjan.lucjanov@gmail.com>
# Contributor: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kwallet-git
_pkgname=kwallet
pkgver=5.64.0.r922.gdf037e3
pkgrel=1
pkgdesc='Secure and unified container for user passwords'
arch=('x86_64')
url='https://community.kde.org/Frameworks'
license=('LGPL')
depends=('knotifications' 'kiconthemes' 'kservice' 'gpgme')
makedepends=('extra-cmake-modules' 'kdoctools' 'boost' 'doxygen' 'qt5-tools' 'qt5-doc' 'git')
optdepends=('kwalletmanager: Configuration GUI')
provides=('kwallet')
conflicts=('kwallet')
groups=('kf5-git')
source=("git://anongit.kde.org/$_pkgname.git")
sha256sums=('SKIP')

pkgver() {
  cd $_pkgname
  _ver="$(cat CMakeLists.txt | grep -m1 'set(KF5_VERSION' | cut -d '"' -f2 | tr - .)"
  echo "${_ver}.r$(git rev-list --count HEAD).g$(git rev-parse --short HEAD)"
}
   
prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${_pkgname} \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
