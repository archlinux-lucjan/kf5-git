# Maintainer: Piotr GÃ³rski <lucjan.lucjanov@gmail.com>
# Contributor: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=knotifications-git
_pkgname=knotifications
pkgver=5.72.0.r546.gf4cd74e
pkgrel=1
pkgdesc='Abstraction for system notifications'
arch=('x86_64')
url='https://community.kde.org/Frameworks'
license=('LGPL')
depends=('libcanberra' 'libdbusmenu-qt5' 'kwindowsystem' 'kconfig' 'kcodecs' 'kcoreaddons' 'qt5-speech')
makedepends=('extra-cmake-modules' 'qt5-tools' 'qt5-doc' 'doxygen' 'git')
provides=('knotifications')
conflicts=('knotifications')
groups=('kf5-git')
source=("git+https://anongit.kde.org/$_pkgname.git")
sha256sums=('SKIP')

pkgver() {
  cd $_pkgname
  _ver="$(cat CMakeLists.txt | grep -m1 'set(KF5_VERSION' | cut -d '"' -f2 | tr - .)"
  echo "${_ver}.r$(git rev-list --count HEAD).g$(git rev-parse --short HEAD)"
}

build() {
  cmake -B build -S $_pkgname \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
