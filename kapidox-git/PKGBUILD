# Maintainer: Piotr GÃ³rski <lucjan.lucjanov@gmail.com>
# Contributor: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kapidox-git
_pkgname=kapidox
pkgver=5.72.0.r477.gb851d68
pkgrel=1
pkgdesc='Frameworks API Documentation Tools'
arch=('any')
url='https://community.kde.org/Frameworks'
license=('LGPL')
depends=('python-jinja' 'python-yaml')
makedepends=('extra-cmake-modules' 'qt5-base' 'git')
provides=('kapidox')
conflicts=('kapidox')
groups=('kf5-git')
source=("git+https://anongit.kde.org/$_pkgname.git")
sha256sums=('SKIP')

pkgver() {
  cd $_pkgname
  _ver="$(cat setup.py | grep -m1 version | grep -o "[[:digit:]]*" | paste -sd'.')"
  echo "${_ver}.r$(git rev-list --count HEAD).g$(git rev-parse --short HEAD)"
}

build() {
  cmake -B build -S $_pkgname \
    -DCMAKE_INSTALL_PREFIX=/usr
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
