# $Id: PKGBUILD 293003 2017-04-16 09:01:56Z arojas $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Antonio Rojas

pkgname=breeze-icons-git
_pkgname=breeze-icons
pkgver=5.39.0.r1040.g1a3aafad
pkgrel=1
pkgdesc='Breeze icon themes'
arch=('any')
url='https://community.kde.org/Frameworks'
license=('LGPL')
groups=('kf5-git')
makedepends=('extra-cmake-modules' 'qt5-base')
provides=('breeze-icons')
conflicts=('breeze-icons')
source=("git://anongit.kde.org/$_pkgname.git")
sha256sums=('SKIP')

pkgver() {
  cd $_pkgname
  _ver="$(cat CMakeLists.txt | grep -m1 'set(KF5_VERSION' | cut -d '"' -f2 | tr - .)"
  echo "${_ver}.r$(git rev-list --count HEAD).g$(git rev-parse --short HEAD)"
}
   
            
prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${_pkgname} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBINARY_ICONS_RESOURCE=ON \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
