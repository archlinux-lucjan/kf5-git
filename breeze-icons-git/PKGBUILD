# Maintainer: Piotr GÃ³rski <lucjan.lucjanov@gmail.com>
# Contributor: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=breeze-icons-git
_pkgname=breeze-icons
pkgver=5.79.0.r1691.g2b9f32e2
pkgrel=1
pkgdesc='Breeze icon themes'
arch=('any')
url='https://community.kde.org/Frameworks'
license=('LGPL')
groups=('kf5-git')
makedepends=('extra-cmake-modules' 'qt5-base' 'git')
provides=('breeze-icons')
conflicts=('breeze-icons')
source=("git+https://anongit.kde.org/$_pkgname.git")
sha256sums=('SKIP')

pkgver() {
  cd $_pkgname
  _ver="$(cat CMakeLists.txt | grep -m1 'set(KF_VERSION' | cut -d '"' -f2 | tr - .)"
  echo "${_ver}.r$(git rev-list --count HEAD).g$(git rev-parse --short HEAD)"
}

build() {
  cmake -B build -S $_pkgname \
    -DBINARY_ICONS_RESOURCE=ON \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
