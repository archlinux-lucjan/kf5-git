# Maintainer: Piotr GÃ³rski <lucjan.lucjanov@gmail.com>
# Contributor: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdelibs4support-git
_pkgname=kdelibs4support
pkgver=5.39.0.r752.g90a00161
pkgrel=1
pkgdesc='Porting aid from KDELibs4'
arch=('i686' 'x86_64')
url='https://community.kde.org/Frameworks'
license=('LGPL')
depends=('kunitconversion' 'kitemmodels' 'kemoticons' 'kded' 'kparts')
makedepends=('extra-cmake-modules' 'kdoctools' 'qt5-tools' 'networkmanager' 'perl-uri' 'python' 'kdesignerplugin')
provides=('kdelibs4support')
conflicts=('kdelibs4support')
groups=('kf5-aids-git')
source=("git://anongit.kde.org/$_pkgname.git"
 kdelibs4support-openssl-1.1.patch)
sha256sums=('SKIP'
            'e33de96fae2b93c3fa06b4219205ae188b214be8c5e84b8d0426217fd65a5d48')

pkgver() {
  cd $_pkgname
  _ver="$(cat CMakeLists.txt | grep -m1 'set(KF5_VERSION' | cut -d '"' -f2 | tr - .)"
  echo "${_ver}.r$(git rev-list --count HEAD).g$(git rev-parse --short HEAD)"
}
           
prepare() {
  mkdir -p build
  cd $_pkgname
  patch -p1 -i ../kdelibs4support-openssl-1.1.patch # Fix build with openssl 1.1
}

build() {
  cd build
  cmake ../${_pkgname} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_LIBEXECDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install

# cert bundle seems to be hardcoded
# link it to the one from ca-certificates
  rm -f "$pkgdir"/usr/share/kf5/kssl/ca-bundle.crt
  ln -sf /etc/ssl/certs/ca-certificates.crt "${pkgdir}"/usr/share/kf5/kssl/ca-bundle.crt
}
