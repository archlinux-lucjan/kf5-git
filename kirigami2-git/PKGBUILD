# Maintainer: Piotr GÃ³rski <lucjan.lucjanov@gmail.com>
# contributor: Antonio Rojas <arojas@archlinux.org>

pkgname=kirigami2-git
_pkgname=kirigami
pkgver=5.79.0.r2632.g2678dcdc
pkgrel=1
pkgdesc='A QtQuick based components set'
arch=(x86_64)
url='https://community.kde.org/Frameworks'
license=(LGPL)
depends=('qt5-quickcontrols' 'qt5-quickcontrols2' 'qt5-graphicaleffects')
makedepends=('extra-cmake-modules' 'qt5-tools' 'qt5-doc' 'qt5-svg' 'kpackage' 'doxygen' 'git')
provides=('kirigami2')
conflicts=('kirigami2')
groups=('kf5-git')
source=("git+https://anongit.kde.org/$_pkgname.git")
sha256sums=('SKIP')

pkgver() {
  cd $_pkgname
  _ver="$(cat CMakeLists.txt | grep -m1 'set(KF_VERSION' | cut -d '"' -f2 | tr - .)"
  echo "${_ver}.r$(git rev-list --count HEAD).g$(git rev-parse --short HEAD)"
}

build() {
  cmake -B build -S $_pkgname \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
