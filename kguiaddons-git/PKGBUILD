# Maintainer: Piotr GÃ³rski <lucjan.lucjanov@gmail.com>
# Contributor: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kguiaddons-git
_pkgname=kguiaddons
pkgver=5.80.0.r340.g337b9a1
pkgrel=1
pkgdesc='Addons to QtGui'
arch=('x86_64')
url='https://community.kde.org/Frameworks'
license=('LGPL')
depends=('qt5-x11extras')
makedepends=('extra-cmake-modules' 'clang' 'python-pyqt5' 'doxygen' 'qt5-tools' 'qt5-doc' 'sip' 'git')
optdepends=('python-pyqt5: for the Python bindings')
provides=('kguiaddons')
conflicts=('kguiaddons')
groups=('kf5-git')
source=("git+https://anongit.kde.org/$_pkgname.git")
sha256sums=('SKIP')

pkgver() {
  cd $_pkgname
  _ver="$(cat CMakeLists.txt | grep -m1 'set(KF_VERSION' | cut -d '"' -f2 | tr - .)"
  echo "${_ver}.r$(git rev-list --count HEAD).g$(git rev-parse --short HEAD)"
}

build() {
  cmake -B build -S $_pkgname \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
